; ----------------------------------------------------------------------------------------------------------------
;
; CreateCollapsedNetwork.job
;
; TP+ script to create a collapsed network from the input MTC TM2 network. The script
; collapses all MAZs within each TAZ outside of a given county code. It first deletes all MAZ
; centroids and MAZ centroid connectors for all MAZs outside the given county. It then
; generates a new centroid next to the TAZ centroid, and generates centroid connectors to all the 
; network links where the centroid connectors are connected for the parent TAZ (essentially
; replicating the TAZ behavior for the MAZ). This script should be called before the script
; zone_seq_net_builder.job for subarea models.
;
; ----------------------------------------------------------------------------------------------------------------

; Select county
county = %SELECT_COUNTY%

;zone is < 900,000 (skip externals)
zone_selector = '(N < 900000)'
;taz is X00,001 - X09,999
taz_selector = '(N % 100000 < 10000)'
;maz is X10,001 - X89,999
maz_selector = '(N % 100000 > 10000) & (N % 100000 < 90000)'
;tap is X90,001 - X99,999
tap_selector = '(N % 100000 > 90000)'

;county codes are X00,000 for zone nodes
; 0 - San Francisco
; 1 - San Mateo
; 2 - Santa Clara
; 3 - Alameda
; 4 - Contra Costa
; 5 - Solano
; 6 - Napa
; 7 - Sonoma
; 8 - Marin
county_selector_sf = '(N < 100000)'
county_selector_sm = '(N < 200000) & (N > 100000)'
county_selector_sc = '(N < 300000) & (N > 200000)'
county_selector_al = '(N < 400000) & (N > 300000)'
county_selector_cc = '(N < 500000) & (N > 400000)'
county_selector_sl = '(N < 600000) & (N > 500000)'
county_selector_np = '(N < 700000) & (N > 600000)'
county_selector_sn = '(N < 800000) & (N > 700000)'
county_selector_mn = '(N < 900000) & (N > 800000)'

taz_all = '(' + zone_selector + '  & ' + taz_selector + ')'
taz_sf = '(' + county_selector_sf + ' & ' + taz_selector + ')'
taz_sm = '(' + county_selector_sm + ' & ' + taz_selector + ')'
taz_sc = '(' + county_selector_sc + ' & ' + taz_selector + ')'
taz_al = '(' + county_selector_al + ' & ' + taz_selector + ')'
taz_cc = '(' + county_selector_cc + ' & ' + taz_selector + ')'
taz_sl = '(' + county_selector_sl + ' & ' + taz_selector + ')'
taz_np = '(' + county_selector_np + ' & ' + taz_selector + ')'
taz_sn = '(' + county_selector_sn + ' & ' + taz_selector + ')'
taz_mn = '(' + county_selector_mn + ' & ' + taz_selector + ')'

maz_all = '(' + zone_selector + '  & ' + maz_selector + ')'
maz_sf = '(' + county_selector_sf + ' & ' + maz_selector + ')'
maz_sm = '(' + county_selector_sm + ' & ' + maz_selector + ')'
maz_sc = '(' + county_selector_sc + ' & ' + maz_selector + ')'
maz_al = '(' + county_selector_al + ' & ' + maz_selector + ')'
maz_cc = '(' + county_selector_cc + ' & ' + maz_selector + ')'
maz_sl = '(' + county_selector_sl + ' & ' + maz_selector + ')'
maz_np = '(' + county_selector_np + ' & ' + maz_selector + ')'
maz_sn = '(' + county_selector_sn + ' & ' + maz_selector + ')'
maz_mn = '(' + county_selector_mn + ' & ' + maz_selector + ')'

; first delete maz connectors outside the chosen county
run pgm = network
    NETI = "hwy\mtc_final_network_base.net"
    NETO = "hwy\mtc_final_network_collapsed_nomazconnectors.net"
		
    PHASE=LINKMERGE
        
		;loop by county 
		LOOP _PROC_COUNT=1,9


			COUNTY_NODE_LOW = ((_PROC_COUNT-1) * 100000) + 10000
			COUNTY_NODE_HI  = ((_PROC_COUNT-1) * 100000) + 90000
   
			; skip the selected county
			if(_PROC_COUNT = @county@) continue

			;maz is X10,001 - X89,999

			; delete MAZ centroid connectors
			if((A.N > COUNTY_NODE_LOW) & (A.N < COUNTY_NODE_HI)) delete
			if((B.N > COUNTY_NODE_LOW) & (B.N < COUNTY_NODE_HI)) delete
			
		ENDLOOP
		    
	ENDPHASE
   
ENDRUN
; now delete maz nodes outside the chosen county
run pgm = network
    NETI = "hwy\mtc_final_network_collapsed_nomazconnectors.net"
    NETO = "hwy\mtc_final_network_collapsed_nomazs.net"
		
    PHASE=NODEMERGE
        
		;loop by county 
		LOOP _PROC_COUNT=1,9


			COUNTY_NODE_LOW = ((_PROC_COUNT-1) * 100000) + 10000
			COUNTY_NODE_HI  = ((_PROC_COUNT-1) * 100000) + 90000
   
			; skip the selected county
			if(_PROC_COUNT = @county@) continue
	
			;maz is X10,001 - X89,999
			
			; delete MAZ centroids
			if((N > COUNTY_NODE_LOW) & (N < COUNTY_NODE_HI)) delete
			
		ENDLOOP
		    
	ENDPHASE
   
ENDRUN
; now create maz nodes and links from centroids outside the chosen county
run pgm = network
    NETI = "hwy\mtc_final_network_collapsed_nomazs.net"
    NODEO = "hwy\newmazcentroids.dbf", FORMAT=DBF, EXCLUDE=COUNTY_NODE_LOW, COUNTY_NODE_HI
	LINKO = "hwy\newmazconnectors.dbf", FORMAT=DBF, EXCLUDE=COUNTY_NODE_LOW, COUNTY_NODE_HI
		
    PHASE=NODEMERGE
	
		;loop by county 
		LOOP _PROC_COUNT=1,9


			COUNTY_NODE_LOW =  (_PROC_COUNT-1) * 100000
			COUNTY_NODE_HI  = ((_PROC_COUNT-1) * 100000) + 10000
   
			; skip the selected county
			if(_PROC_COUNT = @county@) CONTINUE
            
			;taz is X00,001 - X09,999
            ;taz_selector = '(N % 100000 < 10000)

			; if it is an existing TAZ centroid, use the zone sequence within the county to code the MAZ number and offset the x and y
			if((N > COUNTY_NODE_LOW) & (N < COUNTY_NODE_HI)) 
			  
			  IF(_PROC_COUNT>1)
			    TAZ_PORTION = N % ((_PROC_COUNT-1)*100000)
			    NEW_N = (_PROC_COUNT-1)*100000 + 10000 + TAZ_PORTION
			  ELSE
			    TAZ_PORTION = N
				NEW_N = N + 10000
			  ENDIF
			  
			  NEW_X = X + 100.0
			  NEW_Y = Y + 100.0
			  
			ENDIF
	
     	ENDLOOP
	    
	ENDPHASE
	
	PHASE=LINKMERGE
        
		;loop by county 
		LOOP _PROC_COUNT=1,9

			COUNTY_NODE_LOW = (_PROC_COUNT-1)* 100000
			COUNTY_NODE_HI  = _PROC_COUNT * 100000
   
   			; skip the selected county
			if(_PROC_COUNT = @county@) CONTINUE
           
			; if it is a taz connector, change the node that connects to the zone to be the maz node
			if((A.N % 100000 < 10000) & (A.N > COUNTY_NODE_LOW) & (A.N < COUNTY_NODE_HI)) 
			  
			  IF(_PROC_COUNT>1)
			    TAZ_PORTION = A.N % ((_PROC_COUNT-1)*100000)
  			    NEW_A_NODE =(_PROC_COUNT-1) * 100000 + 10000 + TAZ_PORTION
			  ELSE
			    TAZ_PORTION = A.N
				NEW_A_NODE = TAZ_PORTION + 10000
			  ENDIF

		      NEW_B_NODE = B.N
			  CNTYPE='MAZ'

		    endif
			
			if((B.N % 100000 < 10000) & (B.N > COUNTY_NODE_LOW) & (B.N < COUNTY_NODE_HI)) 
			  
			  if(_PROC_COUNT>1)
			    TAZ_PORTION = B.N % ((_PROC_COUNT-1)*100000)
			    NEW_B_NODE =(_PROC_COUNT-1)*100000 + 10000 + TAZ_PORTION
		      ELSE
			    TAZ_PORTION = B.N
				NEW_B_NODE = TAZ_PORTION+10000
			  ENDIF
			  
			  NEW_A_NODE = A.N
			  CNTYPE='MAZ'

		    endif
		    
		ENDLOOP
         
	ENDPHASE

ENDRUN
; now delete non-maz nodes and links 
run pgm = network
    NODEI = "hwy\newmazcentroids.dbf"
	LINKI = "hwy\newmazconnectors.dbf"
    NODEO = "hwy\newmazcentroids_rev.dbf", FORMAT=DBF
	LINKO = "hwy\newmazconnectors_rev.dbf", FORMAT=DBF
		
    PHASE=NODEMERGE
	
	    IF(NEW_N=0) DELETE
		    
	ENDPHASE
	
	PHASE=LINKMERGE
        
        IF((NEW_A_NODE=0) | (NEW_B_NODE=0)) DELETE	
		   
	ENDPHASE

ENDRUN


; now read back in the new maz connector nodes and links and merge with no mazs network to create final network
run pgm = network
    NETI = "hwy\mtc_final_network_collapsed_nomazs.net"
    NODEI[2] = "hwy\newmazcentroids_rev.dbf", RENAME=N-OLD_N,X-OLD_X,Y-OLD_Y,NEW_N-N,NEW_X-X,NEW_Y-Y
	LINKI[2] = "hwy\newmazconnectors_rev.dbf", RENAME=A-OLD_A,B-OLD_B,NEW_A_NODE-A,NEW_B_NODE-B
	NETO = 	"hwy\mtc_final_network_collapsed.net", EXCLUDE=COUNTY_NODE_LOW,COUNTY_NODE_HI,TAZ_PORTION,OLD_A,OLD_B	  

	
   MERGE RECORD = TRUE
	
ENDRUN

*del "hwy\mtc_final_network_base.net"
*copy "hwy\mtc_final_network_collapsed.net" "hwy\mtc_final_network_base.net" 	
*del "hwy\mtc_final_network_collapsed.net" 	  
	
	
	
